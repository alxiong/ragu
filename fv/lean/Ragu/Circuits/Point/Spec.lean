import Clean.Circuit
import Ragu.Core

namespace Ragu.Circuits.Point.Spec
open Core.Primes
variable {p : ℕ} [Fact p.Prime]

structure Point (F : Type) where
  x : F
  y : F
deriving ProvableStruct



/--
  NOTE: supports only short Weierstrass curves with a = 0

  TODO: replace with Weierstrass in Mathlib
-/
structure CurveParams (F : Type) where
  b : F

def Point.isOnCurve (point : Point (F p)) (curveParams : CurveParams (F p)) : Prop :=
  point.y^2 = point.x^3 + curveParams.b

def CurveParams.noOrderTwoPoints (curveParams : CurveParams (F p)) : Prop :=
  (∀ point : Spec.Point (F p), (point.isOnCurve curveParams) → point.y ≠ 0)

def Point.double (point : Point (F p)) : Point (F p) :=
  -- TODO: a / 0 is defined to be 0 for fields, this is not a precise spec
  let lambda := (3 * point.x^2) / (2 * point.y)
  let x2 := lambda^2 - 2*point.x
  {
    x := x2,
    y := lambda * (point.x - x2) - point.y
  }

-- concrete pasta curves parameters
def EpAffineParams: Circuits.Point.Spec.CurveParams Core.Primes.Fp := {b := 5}
def EqAffineParams: Circuits.Point.Spec.CurveParams Core.Primes.Fq := {b := 5}

end Ragu.Circuits.Point.Spec

namespace Ragu.Circuits.Point.Lemmas
variable {p : ℕ} [Fact p.Prime] [NeZero (2 : F p)]
open Spec

lemma double_preserves_membership (point : Point (F p)) (curveParams: CurveParams (F p))
    (h_membership: point.isOnCurve curveParams) (h_order : curveParams.noOrderTwoPoints) :
    point.double.isOnCurve curveParams := by
  simp only [CurveParams.noOrderTwoPoints] at h_order
  specialize h_order point h_membership
  simp only [Point.isOnCurve, Point.double]
  simp only [Point.isOnCurve] at h_membership
  have h2 : (2 : F p) ≠ 0 := NeZero.ne 2
  have h2y : (2 : F p) * point.y ≠ 0 := mul_ne_zero h2 h_order
  field_simp [h_order, h2y, h2]
  have hb : curveParams.b = point.y ^ 2 - point.x ^ 3 := by rw [h_membership]; ring
  rw [hb]
  ring


end Ragu.Circuits.Point.Lemmas
